plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm" version "1.8.22"
    id "com.exactpro.th2.gradle.component" version "0.1.1"
}

ext {
    sailfishVersion = '3.3.241'
}

ext.excludeSailfish = { rcd ->
    rcd.excludeModule("com.exactpro.sf", "sailfish-core")
    rcd.excludeModule("com.exactpro.sf", "sailfish-common")
    rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
    rcd.excludeModule("com.exactpro.sf", "service-http")
}

group = 'com.exactpro.th2'
version = release_version

repositories {
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        content {
            excludeSailfish(it)
        }
    }
    // ignoreGradleMetadataRedirection is used for sonatype because
    // Sailfish dependencies have constrains that interfere with our BOM
    // so we exclude Gradle metadata for this repositories.
    // We've checked these versions - they are compatible and safe to use
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        content {
            excludeSailfish(it)
        }
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    mavenCentral()
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

kotlin {
    jvmToolchain(11)
}

dependencies {
    implementation 'com.exactpro.th2:common:5.14.0-dev'
    implementation 'com.exactpro.th2:sailfish-utils:4.1.1-dev'
    implementation 'com.exactpro.th2:common-utils:2.2.3-dev'

    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.8'

    implementation 'org.slf4j:slf4j-api'

    implementation 'io.prometheus:simpleclient'

    implementation("com.exactpro.sf:sailfish-core:${sailfishVersion}")

    testImplementation('org.apache.logging.log4j:log4j-slf4j2-impl') {
        because('logging in testing')
    }
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

application {
    mainClass.set('com.exactpro.th2.conn.MicroserviceMain')
}