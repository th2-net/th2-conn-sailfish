plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id "de.undercouch.download" version "4.0.4" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id "com.jfrog.artifactory" version '4.15.1'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'java-library'

    ext {
        sharedDir                       = file("${project.rootDir}/shared")
    }

    group = 'com.exactpro.th2'
    version = release_version

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        maven {
            name 'Artifactory release'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-release'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        api platform('com.exactpro.th2:bom:2.0.+')
    }


    jar {
        manifest {
            attributes(
                    'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                    'Specification-Title':      '',
                    'Specification-Vendor':     'Exactpro Systems LLC',
                    'Implementation-Title':     project.archivesBaseName,
                    'Implementation-Vendor':    'Exactpro Systems LLC',
                    'Implementation-Vendor-Id': 'com.exactpro',
                    'Implementation-Version':   project.version
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    artifactory {
        publish {
            contextUrl = "${artifactory_url}"
            repository {
                repoKey = "${artifactory_deploy_repo_key}" // The Artifactory repository key to publish to
                username = "${artifactory_user}" // The publisher user name
                password = "${artifactory_password}" // The publisher password
            }
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = true
                // Properties to be attached to the published artifacts.
                publishPom = true
            }
        }
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.palantir.docker'
apply plugin: 'java-library'

ext {
    sailfishVersion                 = '3.2-SNAPSHOT'
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

dependencies {
    implementation(project(':grpc-conn'))

    implementation "com.exactpro.th2:common"
    implementation "com.exactpro.th2:sailfish-utils"
    implementation "com.exactpro.th2:store-common"
    implementation "com.exactpro.th2:grpc-estore"
    implementation "com.exactpro.cradle:cradle-cassandra"
    implementation "com.exactpro.cradle:cradle-core"

    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.0.4'

    implementation "com.rabbitmq:amqp-client:5.8.0"

    implementation "com.exactpro.sf:sailfish-core:${sailfishVersion}"

    testImplementation 'junit:junit:4.12'
}

artifactoryPublish.skip = true

application {
    mainClassName 'com.exactpro.th2.MicroserviceMain'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
